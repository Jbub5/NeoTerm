apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
  compileSdkVersion rootProject.ext.android.COMPILE_SDK_VERSION
  ndkVersion "26.3.11579264"

  defaultConfig {
    applicationId "com.neoterm"
    namespace 'com.neoterm'
    minSdkVersion rootProject.ext.android.MIN_SDK_VERSION
    targetSdkVersion rootProject.ext.android.TARGET_SDK_VERSION
    versionCode 38
    versionName "2.0.5"
    testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    resConfigs "en"

    externalNativeBuild {
      cmake {
        cFlags "-std=c11", "-Wall", "-Wextra", "-Werror", "-Os", "-fno-stack-protector"
        abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'
        arguments "-DCMAKE_BUILD_TYPE=Release", "-DCMAKE_SHARED_LINKER_FLAGS=-flto -Xlinker --gc-sections -Xlinker --strip-all"
      }
    }

    splits {
      density {
        enable false
        reset()
        include "mdpi", "hdpi", "xhdpi", "xxhdpi", "xxxhdpi"
      }
      abi {
        enable true
        reset()
        include 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        universalApk true
      }
    }

    sourceSets {
      main {
        jniLibs.srcDirs = ['src/main/jniLibs']
      }
    }
  }

  buildTypes {
    release {
      zipAlignEnabled true
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.debug

      externalNativeBuild {
        cmake {
          cFlags "-mtune=generic -flto -fvisibility=hidden", "-g0 -ggdb0 -fno-ident -fno-dwarf2-cfi-asm -gno-modules -gno-embed-source -gno-simple-template-names -gno-record-command-line -gno-record-gcc-switches -gno-column-info -gno-gnu-pubnames -gno-pubnames -fno-debug-types-section -fno-var-tracking -fno-stack-protector -ffunction-sections -fdata-sections"
        }
      }

    }

    debug {
      debuggable true
      zipAlignEnabled true
      minifyEnabled false
      shrinkResources false
      signingConfig signingConfigs.debug

      externalNativeBuild {
        cmake {
          cFlags "-g"
          arguments "-DCMAKE_BUILD_TYPE=Debug", "-DCMAKE_SHARED_LINKER_FLAGS=-Xlinker --gc-sections"
        }
      }
    }

  }
  externalNativeBuild {
    cmake {
      path file('src/main/jni/CMakeLists.txt')
    }
  }

  compileOptions {
    targetCompatibility 1.8
    sourceCompatibility 1.8
  }

  kotlinOptions {
    freeCompilerArgs = ["-Xallow-result-return-type"]
  }

  lint {
    abortOnError false
    checkReleaseBuilds false
  }

    packagingOptions {
        jniLibs {
            useLegacyPackaging true
        }
        resources {
            excludes += ["kotlin-tooling-metadata.json", "META-INF/*.version", "drawable-ldrtl-*dpi"]
        }
    }

}

dependencies {
  implementation fileTree(include: ['*.jar'], dir: 'libs')
  testImplementation rootProject.ext.deps["junit"]
  androidTestImplementation project(path: ':NeoLang')

  implementation rootProject.ext.deps["kotlin-stdlib"]

  implementation 'org.greenrobot:eventbus:3.3.1'
  implementation 'com.github.wrdlbrnft:modular-adapter:0.3.0.22'
  implementation 'com.github.wrdlbrnft:sorted-list-adapter:0.3.0.27'
  implementation 'com.simplecityapps:recyclerview-fastscroll:1.0.16'
  implementation 'com.github.GrenderG:Color-O-Matic:1.1.5'

  implementation 'androidx.annotation:annotation:1.3.0'
  implementation 'androidx.cardview:cardview:1.0.0'
  implementation 'androidx.appcompat:appcompat:1.3.1'
  implementation 'androidx.appcompat:appcompat-resources:1.3.1'
  implementation 'androidx.preference:preference-ktx:1.1.1'

  implementation project(':chrome-tabs')
  implementation project(':NeoLang')
  implementation project(':NeoTermBridge')
}

java {
  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17
}

///c/Program\ Files/Android/Android\ Studio/jbr/bin/java.exe -jar /c/Users/User/AndroidStudioProjects/bundletool-all-1.17.0.jar build-apks --bundle=/c/Users/User/AndroidStudioProjects/NeoTerm/app/build/outputs/bundle/release/app-release.aab --output=/c/Users/User/AndroidStudioProjects/NeoTerm/app/build/outputs/bundle/release/app-release.apks --mode=universal
